{-# LANGUAGE DataKinds #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE TypeOperators, TypeApplications #-}
{-# LANGUAGE OverloadedLabels #-}
{-# OPTIONS_GHC -Wno-unrecognised-pragmas #-}
{-# HLINT ignore "Redundant return" #-}
module Tests.Examples.SIR where

import Prog
import Effects.Writer
import Model
import Inference.SIM as SIM
import Inference.MH as MH
import Sampler
import Env
import Control.Monad

import Examples.HMM
import Data.Extensible (Associated)

-- ||| (Section 3.1 + Section 5.5 extension) The SIR model

data Popl = Popl {
    s   :: Int, -- ^ Number of people susceptible to infection
    i   :: Int, -- ^ Number of people currently infected
    r   :: Int  -- ^ Number of people recovered from infection
} deriving Show

type Reported = Int

-- | SIR transition model
transSI :: TransModel env ts Double Popl
transSI beta (Popl s i r) = do
  let pop = s + i + r
  dN_SI <- binomial' s (1 - exp ((-beta * fromIntegral i) / fromIntegral pop))
  return $ Popl (s - dN_SI) (i + dN_SI) r

transIR :: TransModel env ts Double Popl
transIR gamma (Popl s i r)  = do
  dN_IR <- binomial' i (1 - exp (-gamma))
  return $ Popl s (i - dN_IR) (r + dN_IR)

data TransParamsSIR = TransParamsSIR {
    betaP  :: Double, -- ^ Mean contact rate between susceptible and infected people
    gammaP :: Double  -- ^ Mean recovery rate
}

transSIR :: Member (Writer [Popl]) es -- || Writer effect from Section 5.5
  => TransModel env es TransParamsSIR Popl
transSIR (TransParamsSIR beta gamma) sir = do
  sir' <- (transSI beta >=> transIR gamma) sir
  tellM [sir'] 
  return sir'

-- | SIR observation model
type ObsParams = Double

obsSIR :: Observable env "ùúâ" Int
  => ObsModel env ts Double Popl Reported
obsSIR rho (Popl _ i _)  = do
  i <- poisson (rho * fromIntegral i) #ùúâ
  return i

-- | SIR transition prior
transPriorSIR :: Observables env '["Œ≤",  "Œ≥"] Double
  => Model env ts TransParamsSIR
transPriorSIR = do
  pBeta  <- gamma 2 1 #Œ≤
  pGamma <- gamma 1 (1/8) #Œ≥
  return (TransParamsSIR pBeta pGamma)

-- | SIR observation prior
obsPriorSIR :: Observables env '["œÅ"] Double
  => Model env ts ObsParams
obsPriorSIR = do
  pRho <- beta 2 7 #œÅ
  return pRho

-- | SIR as HMM
hmmSIR :: (Member (Writer [Popl]) es, Observable env "ùúâ" Int, Observables env '["œÅ", "Œ≤", "Œ≥"] Double)
  => Int -> Popl -> Model env es Popl
hmmSIR = hmmGen transPriorSIR obsPriorSIR transSIR obsSIR

hmmSIR' :: (Observables env '["ùúâ"] Int , Observables env '[ "Œ≤" , "Œ≥" , "œÅ"] Double) => Int -> Popl -> Model env es (Popl, [Popl])
hmmSIR' n = handleWriterM . hmmSIR n

type SIRenv = '["Œ≤" := Double, "Œ≥"  := Double, "œÅ"  := Double, "ùúâ" := Int]

-- ||| (Section 3.1, Fig 4a) SIM from SIR model: ([(s, i, r)], [ùúâ])
simSIR :: Int -> Sampler ([(Int, Int, Int)], [Reported])
simSIR n_days = do
  let sim_env_in = #Œ≤ := [0.7] <:> #Œ≥ := [0.009] <:> #œÅ := [0.3] <:> #ùúâ := [] <:> enil
      sir_0      = Popl {s = 762, i = 1, r = 0}
  ((_, sir_trace), sim_env_out) <- SIM.simulate (hmmSIR' n_days sir_0) sim_env_in 
  let ùúâs :: [Reported] = get #ùúâ sim_env_out
      sirs = map (\(Popl s i recov) -> (s, i, recov)) sir_trace
  return (sirs, ùúâs)

-- ||| (Section 3.3, Fig 5) Infer from SIR model: ([œÅ], [Œ≤])
mhSIR :: Int -> Int -> Sampler ([Double], [Double])
mhSIR n_mhsteps n_days = do
  ùúâs <- snd <$> simSIR n_days
  let mh_env_in = #Œ≤ := [] <:> #Œ≥ := [0.0085] <:> #œÅ := [] <:> #ùúâ := ùúâs <:> enil
      sir_0           = Popl {s = 762, i = 1, r = 0}
  mhTrace <- MH.mh n_mhsteps (hmmSIR' n_days sir_0) mh_env_in ["Œ≤", "œÅ"]
  let œÅs = concatMap (get #œÅ) mhTrace
      Œ≤s = concatMap (get #Œ≤) mhTrace
  return (œÅs, Œ≤s)